ctf_sec

high

# An early user can manipulate the price per share and profit from late users' deposits In the vault.

## Summary

A well known attack vector for almost all shares based liquidity pool contracts, where an early user can manipulate the price per share and profit from late users' deposits because of the precision loss caused by the rather large value of price per share.

## Vulnerability Detail

A malicious early user can deposit() with 1 wei of asset token as the first depositor of the Token, and get 1 wei of shares.

Then the attacker can send 10000e18 - 1 of asset tokens and inflate the price per share from 1.0000 to an extreme value of 1.0000e22 ( from (1 + 10000e18 - 1) / 1) .

As a result, the future user who deposits 19999e18 will only receive 1 wei (from 19999e18 * 1 / 10000e18) of shares token.

They will immediately lose 9999e18 or half of their deposits if they redeem() right after the deposit().

## Impact

The attacker can profit from future users' deposits. While the late users will lose part of their funds to the attacker.

## Code Snippet

The code is in ERC4626-Cloned.

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/ERC4626-Cloned.sol#L33

```solidity
  function deposit(uint256 assets, address receiver)
    public
    virtual
    override(IVault)
    returns (uint256 shares)
  {
    // Check for rounding error since we round down in previewDeposit.
    require((shares = previewDeposit(assets)) != 0, "ZERO_SHARES");

    // Need to transfer before minting or ERC777s could reenter.
    ERC20(underlying()).safeTransferFrom(msg.sender, address(this), assets);

    _mint(receiver, shares);

    emit Deposit(msg.sender, receiver, assets, shares);

    afterDeposit(assets, shares);
  }
```

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/ERC4626-Cloned.sol#L142

```solidity
  function previewDeposit(uint256 assets)
    public
    view
    virtual
    returns (uint256)
  {
    return convertToShares(assets);
  }
```

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/ERC4626-Cloned.sol#L120

```solidity
  function convertToShares(uint256 assets)
    public
    view
    virtual
    returns (uint256)
  {
    uint256 supply = totalSupply(); // Saves an extra SLOAD if totalSupply is non-zero.

    return supply == 0 ? assets : assets.mulDivDown(supply, totalAssets());
  }
```

## Tool used

Manual Review

## Recommendation

Consider requiring a minimal amount of share tokens to be minted for the first minter, and send a port of the initial mints as a reserve to the DAO so that the pricePerShare can be more resistant to manipulation.
