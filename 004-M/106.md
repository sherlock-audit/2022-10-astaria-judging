TurnipBoy

high

# Bidder will lose all their funds if they place a bid and owner later cancels auction

## Summary

Bidders are allowed to place bids that are below the auction reserve price. If a bidder places a bid lower than reserve price, they will not receive a refund should the owner cancel the auction.

## Vulnerability Detail

    function createBid(uint256 tokenId, uint256 amount) external override {
      address lastBidder = auctions[tokenId].bidder;
      uint256 currentBid = auctions[tokenId].currentBid;
      uint256 duration = auctions[tokenId].duration;
      uint64 firstBidTime = auctions[tokenId].firstBidTime;
      require(
        firstBidTime == 0 || block.timestamp < firstBidTime + duration,
        "Auction expired"
      );
      require(
        amount > currentBid + ((currentBid * minBidIncrementPercentage) / 100),
        "Must send more than last bid by minBidIncrementPercentage amount"
      );
      uint256 vaultPayment = (amount - currentBid);

      if (firstBidTime == 0) {
        auctions[tokenId].firstBidTime = block.timestamp.safeCastTo64();
      } else if (lastBidder != address(0)) {
        uint256 lastBidderRefund = amount - vaultPayment;
        _handleOutGoingPayment(lastBidder, lastBidderRefund);
      }

      _handleIncomingPayment(tokenId, vaultPayment, address(msg.sender));

      auctions[tokenId].currentBid = amount;
      auctions[tokenId].bidder = address(msg.sender);
      ...
    }

In `AuctionHouse.sol#createBid` it allows a user to create a bid that is lower than the reserve price of the auction, since there is no check that the bid being submitted is greater than the reserve price. 

    function cancelAuction(uint256 auctionId, address canceledBy)
      external
      requiresAuth
    {
      require(
        auctions[auctionId].currentBid < auctions[auctionId].reservePrice,
        "cancelAuction: Auction is at or above reserve"
      );
        _handleIncomingPayment(
        auctionId,
        auctions[auctionId].reservePrice,
        canceledBy
      );
      _cancelAuction(auctionId);
    }

In `AuctionHouse.sol#cancelAuction` it only checks that the current bid is less than the reserve price but never sends a refund to the user that has already placed the bid. Instead it calls `_handleIncomingPayment` with the entire reserve price. The bidder never gets a refund and has now effectively lost all the funds they bid.

## Impact

Bidder loses all their funds

## Code Snippet

https://github.com/AstariaXYZ/astaria-gpl/blob/64acee1122a71b23eef037f69cef4c0c087241be/src/AuctionHouse.sol#L210-L224

## Tool used

Manual Review

## Recommendation

The structure of `cancelAuction` should mirror the payment section of `createBid`, first refunding the currentBidder if there is one then handling the difference as an incoming payment.
