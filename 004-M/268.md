Jeiwan

high

# A borrower can steal auctioned collateral for current bidder

## Summary
A borrower can steal auctioned collateral for current bidder
## Vulnerability Detail
A borrower can cancel an auction that already has some bids from other participants ([AuctionHouse.sol#L215](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L215)). When this happens:
1. The borrower is expected to repay the full debt ([AuctionHouse.sol#L220](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L220)):
    ```solidity
    _handleIncomingPayment(
      auctionId,
      auctions[auctionId].reservePrice,
      canceledBy
    );
    ```
    (`reservePrice` is the sum of debts of all borrower's liens)
1. However, some liens have already been repaid by bidders, so the borrower only repays the remaining liens ([AuctionHouse.sol#L280-L299](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L280-L299)):
    ```solidity
    if (liens.length > 0) {
      for (uint256 i = 0; i < liens.length; ++i) {
        uint256 payment;
        uint256 lienId = liens[i];

        ILienToken.Lien memory lien = LIEN_TOKEN.getLien(lienId);

        if (transferAmount >= lien.amount) {
          payment = lien.amount;
          transferAmount -= payment;
        } else {
          payment = transferAmount;
          transferAmount = 0;
        }

        if (payment > 0) {
          LIEN_TOKEN.makePayment(tokenId, payment, lien.position, payer);
        }
      }
    } else {
    ```
1. The current bidder doesn't get refunded.
1. The borrower gets their collateral back at a cheaper price. The closer the current bid to the `reservePrice`, the cheaper it's for the borrower to steal the collateral.

## Impact
A borrower can get their collateral at a cheap price, while the current bidder doesn't get refunded and doesn't receive collateral.
## Code Snippet
[CollateralToken.sol#L331](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/CollateralToken.sol#L331):
```solidity
function cancelAuction(uint256 tokenId) external onlyOwner(tokenId) {
  require(AUCTION_HOUSE.auctionExists(tokenId), "Auction doesn't exist");

  AUCTION_HOUSE.cancelAuction(tokenId, msg.sender);
}
```
[AuctionHouse.sol#L215](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L215):
```solidity
function cancelAuction(uint256 auctionId, address canceledBy)
  external
  requiresAuth
{
  require(
    auctions[auctionId].currentBid < auctions[auctionId].reservePrice,
    "cancelAuction: Auction is at or above reserve"
  );
  _handleIncomingPayment(
    auctionId,
    auctions[auctionId].reservePrice,
    canceledBy
  );
  _cancelAuction(auctionId);
}
```
[AuctionHouse.sol#L253](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L253):
```solidity
function _handleIncomingPayment(
  uint256 tokenId,
  uint256 transferAmount,
  address payer
) internal {
  require(transferAmount > uint256(0), "cannot send nothing");

  Auction storage auction = auctions[tokenId];

  //fee is in percent
  //muldiv?
  //        uint256 initiatorPayment = (transferAmount * auction.initiatorFee) / 100;
  uint256 initiatorPayment = transferAmount.mulDivDown(
    auction.initiatorFee,
    100
  ); //maybe consider making protocl computed like other fees

  TRANSFER_PROXY.tokenTransferFrom(
    weth,
    payer,
    auction.initiator,
    initiatorPayment
  );
  transferAmount -= initiatorPayment;

  uint256[] memory liens = LIEN_TOKEN.getLiens(tokenId);
  uint256 totalLienAmount = 0;
  if (liens.length > 0) {
    for (uint256 i = 0; i < liens.length; ++i) {
      uint256 payment;
      uint256 lienId = liens[i];

      ILienToken.Lien memory lien = LIEN_TOKEN.getLien(lienId);

      if (transferAmount >= lien.amount) {
        payment = lien.amount;
        transferAmount -= payment;
      } else {
        payment = transferAmount;
        transferAmount = 0;
      }

      if (payment > 0) {
        LIEN_TOKEN.makePayment(tokenId, payment, lien.position, payer);
      }
    }
  } else {
    TRANSFER_PROXY.tokenTransferFrom(
      weth,
      payer,
      COLLATERAL_TOKEN.ownerOf(tokenId),
      transferAmount
    );
  }
}
```

## Tool used
Manual Review

## Recommendation
Consider disallowing a borrower to cancel an auction after at least one bid has been made. Or, consider refunding the current bid when a borrower cancels an auction, similarly to how it's done during bidding ([AuctionHouse.sol#L113-L116](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L113-L116)).