TurnipBoy

medium

# Collateral will still have unpaid liens after cancelAuction if initiatorFee != 0

## Summary

When calling `cancelAuction`, `_handleIncomingPayment` is called with the reserve price of the auction (i.e. the value of all open liens). If initiatorFee != 0 then the initiator is paid their fee and the value of the payment is reduced by the fee. Since a fee is taken, the payment will no longer be large enough to pay off all the liens, leaving the final lien with a balance equal to the fee paid to the initiator. 

## Vulnerability Detail

    function createAuction(
      uint256 tokenId,
      uint256 duration,
      address initiator,
      uint256 initiatorFee
    ) external requiresAuth returns (uint256 reserve) {
      (reserve, ) = LIEN_TOKEN.stopLiens(tokenId);

      Auction storage newAuction = auctions[tokenId];
      newAuction.duration = duration.safeCastTo64();

      newAuction.reservePrice = reserve;
      newAuction.initiator = initiator;
      newAuction.initiatorFee = initiatorFee;
      newAuction.firstBidTime = block.timestamp.safeCastTo64();
      newAuction.maxDuration = (duration + 1 days).safeCastTo64();
      newAuction.currentBid = 0;

      emit AuctionCreated(tokenId, duration, reserve);
    }

When creating an auction the reserve price is set to the total amount owed to liens and is not adjusted for the initiator fee.

    function cancelAuction(uint256 auctionId, address canceledBy)
      external
      requiresAuth
    {
      require(
        auctions[auctionId].currentBid < auctions[auctionId].reservePrice,
        "cancelAuction: Auction is at or above reserve"
      );
      _handleIncomingPayment(
        auctionId,
        auctions[auctionId].reservePrice,
        canceledBy
      );
      _cancelAuction(auctionId);
    }

In `AuctionHouse.sol#cancelAuction`, `_handleIncomingPayment` is called with the reserve price of the auction (i.e. the value of all open liens) to be paid by the owner of the collateral. 

    function _handleIncomingPayment(
      uint256 tokenId,
      uint256 transferAmount,
      address payer
    ) internal {
      require(transferAmount > uint256(0), "cannot send nothing");

      Auction storage auction = auctions[tokenId];

      uint256 initiatorPayment = (transferAmount * auction.initiatorFee) / 100;
      uint256 initiatorPayment = transferAmount.mulDivDown(
        auction.initiatorFee,
        100
      );

      TRANSFER_PROXY.tokenTransferFrom(
        weth,
        payer,
        auction.initiator,
        initiatorPayment
      );
      transferAmount -= initiatorPayment;

      uint256[] memory liens = LIEN_TOKEN.getLiens(tokenId);
      uint256 totalLienAmount = 0;
      if (liens.length > 0) {
        for (uint256 i = 0; i < liens.length; ++i) {
          uint256 payment;
          uint256 lienId = liens[i];

          ILienToken.Lien memory lien = LIEN_TOKEN.getLien(lienId);

          if (transferAmount >= lien.amount) {
            payment = lien.amount;
            transferAmount -= payment;
          } else {
            payment = transferAmount;
            transferAmount = 0;
          }

          if (payment > 0) {
            LIEN_TOKEN.makePayment(tokenId, payment, lien.position, payer);
          }
        }
      } else {
        TRANSFER_PROXY.tokenTransferFrom(
          weth,
          payer,
          COLLATERAL_TOKEN.ownerOf(tokenId),
          transferAmount
        );
      }
    }

In `AuctionHouse.sol#_handleIncomingPayment`, the initiator is paid their fee before any lien holders are paid. Since it is being called with the exact amount required to repay lien holders, the payment less fees will no longer cover the full amount of the lien. The auction will be marked complete but there will still be open liens against the collateral.

## Impact

The auction will be cancelled but the collateral will still have open liens against it

## Code Snippet

https://github.com/AstariaXYZ/astaria-gpl/blob/64acee1122a71b23eef037f69cef4c0c087241be/src/AuctionHouse.sol#L250-L304

## Tool used

Manual Review

## Recommendation

When creating an auction adjust the reserve price to account for the initiator fee