rvierdiiev

medium

# AuctionHouse.createBid will revert when bid is took when `auctions[tokenId].maxDuration - auctions[tokenId].duration < timeBuffer' with oferflow error

## Summary
`AuctionHouse.createBid` will revert when bid is took when `auctions[tokenId].maxDuration - auctions[tokenId].duration < timeBuffer', because of incorrect code.
## Vulnerability Detail
```solidity
  function createBid(uint256 tokenId, uint256 amount) external override {
    address lastBidder = auctions[tokenId].bidder;
    uint256 currentBid = auctions[tokenId].currentBid;
    uint256 duration = auctions[tokenId].duration;
    uint64 firstBidTime = auctions[tokenId].firstBidTime;
    require(
      firstBidTime == 0 || block.timestamp < firstBidTime + duration,
      "Auction expired"
    );
    require(
      amount > currentBid + ((currentBid * minBidIncrementPercentage) / 100),
      "Must send more than last bid by minBidIncrementPercentage amount"
    );


    // If this is the first valid bid, we should set the starting time now.
    // If it's not, then we should refund the last bidder
    uint256 vaultPayment = (amount - currentBid);


    if (firstBidTime == 0) {
      auctions[tokenId].firstBidTime = block.timestamp.safeCastTo64();
    } else if (lastBidder != address(0)) {
      uint256 lastBidderRefund = amount - vaultPayment;
      _handleOutGoingPayment(lastBidder, lastBidderRefund);
    }


    _handleIncomingPayment(tokenId, vaultPayment, address(msg.sender));


    auctions[tokenId].currentBid = amount;
    auctions[tokenId].bidder = address(msg.sender);


    bool extended = false;
    // at this point we know that the timestamp is less than start + duration (since the auction would be over, otherwise)
    // we want to know by how much the timestamp is less than start + duration
    // if the difference is less than the timeBuffer, increase the duration by the timeBuffer
    if (firstBidTime + duration - block.timestamp < timeBuffer) {
      // Playing code golf for gas optimization:
      // uint256 expectedEnd = auctions[auctionId].firstBidTime.add(auctions[auctionId].duration);
      // uint256 timeRemaining = expectedEnd.sub(block.timestamp);
      // uint256 timeToAdd = timeBuffer.sub(timeRemaining);
      // uint256 newDuration = auctions[auctionId].duration.add(timeToAdd);


      //TODO: add the cap to the duration, do not let it extend beyond 24 hours extra from max duration
      uint64 newDuration = uint256(
        duration + (block.timestamp + timeBuffer - firstBidTime)
      ).safeCastTo64();
      if (newDuration <= auctions[tokenId].maxDuration) {
        auctions[tokenId].duration = newDuration;
      } else {
        auctions[tokenId].duration =
          auctions[tokenId].maxDuration -
          firstBidTime;
      }
      extended = true;
    }


    emit AuctionBid(
      tokenId,
      msg.sender,
      amount,
      lastBidder == address(0), // firstBid boolean
      extended
    );


    if (extended) {
      emit AuctionDurationExtended(tokenId, auctions[tokenId].duration);
    }
  }
```

`AuctionHouse.createBid` allows to continue auction for `timeBuffer` amount of time if bid was created less then `timeBuffer` seconds till the end of auction.

But in case if `newDuration > auctions[tokenId].maxDuration` then it should not allow auction to last more time than `auctions[tokenId].maxDuration`.
Such code is used to do that.
https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L141-L143
```solidity
auctions[tokenId].duration =
          auctions[tokenId].maxDuration -
          firstBidTime;
```

The problem is that `auctions[tokenId].maxDuration` is a period(for example 3 days), while `firstBidTime` is timestamp(specific time). In this case `auctions[tokenId].maxDuration - firstBidTime` will always revert.
This problem occurs because developer thinks that `auctions[tokenId].maxDuration` is timestamp also. But it is [not like that](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L81).
## Impact
Not possible to make bid that when `auctions[tokenId].maxDuration - auctions[tokenId].duration < timeBuffer'. 
## Code Snippet
Provide above
## Tool used

Manual Review

## Recommendation
Handle `maxDuration` as period, not timestamp.