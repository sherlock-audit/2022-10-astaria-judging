TurnipBoy

high

# WithdrawProxy.sol exchange rate can be abused to steal all funds from withdrawing LPs

## Summary

Shares of `WithdrawProxy.sol` are only meant to be minted by the `WithdrawProxy.sol#mint` function but it doesn't disable the `ERC4626-Cloned.sol#deposit` or `mint` functions. The vault should contain no underlying before the vault is ready to be claimed which would normally prevent this. After legitimate shares have been minted to withdrawing LPs an attacker can donate underlying to the vault then mint a huge number of shares at a tiny exchange ratio. Once the vault is sent the LPs funds the attacker can redeem their shares to steal the funds.

## Vulnerability Detail

    function redeemFutureEpoch(
      uint256 shares,
      address receiver,
      address owner,
      uint64 epoch
    ) public virtual returns (uint256 assets) {
      // check to ensure that the requested epoch is not the current epoch or in the past
      require(epoch >= currentEpoch, "Exit epoch too low");

      require(msg.sender == owner, "Only the owner can redeem");
      // check for rounding error since we round down in previewRedeem.

      ERC20(address(this)).safeTransferFrom(owner, address(this), shares);

      // Deploy WithdrawProxy if no WithdrawProxy exists for the specified epoch
      _deployWithdrawProxyIfNotDeployed(epoch);

      emit Withdraw(msg.sender, receiver, owner, assets, shares);

      // WithdrawProxy shares are minted 1:1 with PublicVault shares
      WithdrawProxy(withdrawProxies[epoch]).mint(receiver, shares); // was withdrawProxies[withdrawEpoch]
    }

When an LP calls `PublicVault.sol#redeemFutureEpoch` their current shares are taken and they are minted empty shares to the current withdraw proxy.

    function claim() public {
      require(
        block.timestamp > finalAuctionEnd || finalAuctionEnd == uint256(0),
        "final auction has not ended"
      );

      uint256 balance = ERC20(underlying()).balanceOf(address(this));
      // would happen if there was no WithdrawProxy for current epoch
      if (withdrawRatio == uint256(0)) {
        ERC20(underlying()).safeTransfer(VAULT(), balance);
      } else {
        //should be wad multiplication
        // declining
        uint256 transferAmount = withdrawRatio.mulDivDown(balance, 1e18);

        if (transferAmount > uint256(0)) {
          ERC20(underlying()).safeTransfer(WITHDRAW_PROXY(), transferAmount);
        }

        unchecked {
          balance -= transferAmount;
        }

        ERC20(underlying()).safeTransfer(VAULT(), balance);
      }

      PublicVault(VAULT()).decreaseYIntercept(
        (expected - ERC20(underlying()).balanceOf(address(this))).mulDivDown(
          1e18 - withdrawRatio,
          1e18
        )
      );
    }

No underlying is present in the withdrawProxy until `Liquidation.sol#claim` sends underlying to the withdraw vault. Between `PublicVault.sol#redeemFutureEpoch` and `Liquidation.sol#claim` there will be shares minted to the vault but no underlying present. This is opens up an attack vector.

    function mint(uint256 shares, address receiver)
      public
      virtual
      returns (uint256 assets)
    {
      assets = previewMint(shares); // No need to check for rounding error, previewMint rounds up.

      // Need to transfer before minting or ERC777s could reenter.
      ERC20(underlying()).safeTransferFrom(msg.sender, address(this), assets);

      _mint(receiver, shares);

      emit Deposit(msg.sender, receiver, assets, shares);

      afterDeposit(assets, shares);
    }

    function previewMint(uint256 shares) public view virtual returns (uint256) {
      uint256 supply = totalSupply(); // Saves an extra SLOAD if totalSupply is non-zero.

      return supply == 0 ? shares : shares.mulDivUp(totalAssets(), supply);
    }

    function totalAssets() public view override returns (uint256) {
      return ERC20(underlying()).balanceOf(address(this));
    }

`WithdrawProxy.sol` inherits from `ERC4626-Cloned.sol` which contains the `mint` method called above. `mint` first calls `previewMint` which calculates the number of assets to required to mint a given number of shares. In this case it takes the target number of shares multiplied by the `totalAssets` (which is the balance of the underlying) and then divides it by the total number of shares outstanding. An attacker can abuse this to mint themselves an extremely large number of shares:

Assume 200e18 legitimate shares have been minted. An attacker can donate 1 underlying to the withdraw contract. Now they call `ERC4626-Cloned.sol#mint` requesting to mint 2e36 shares. Since `totalSupply != 0`, shares are calculated as:

assets = 2e36 * 1 / 200e18 = 1e16

The attacker has now minted 2e36 shares for only 1e16 of the underlying. When `Liquidation.sol#claim` is called, underlying will be transferred to the `WithdrawProxy` and the attacker will be able to claim almost all of it since they own >99.999% of all outstanding shares.

## Impact

All withdrawing LPs will have their funds stolen due to share dilution

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/WithdrawProxy.sol#L79-L82

## Tool used

Manual Review

## Recommendation

WithdrawProxy.sol should override `ERC4626-Cloned.sol#mint` and `deposit` to revert when called or remove all logic from them.