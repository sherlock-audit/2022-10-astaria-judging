TurnipBoy

high

# Attacker can permanently break PublicVault.sol#processEpoch by calling deposit on withdrawProxy.sol

## Summary

In `PublicVault.sol` when an LP wants with redeem their shares they call `redeem` which transfers the PublicVault.sol shares to itself. When calling `PublicVault.sol#processEpoch`, it attempts to burn the `totalSupply` of `withdrawProxy.sol`. Since redeem exchanges 1:1 this normally isn't an issue. `withdrawProxy.sol` leaves the `ERC4626-Cloned.sol#mint` and `deposit` untouched allowing an attacker to call them to mint shares to `withdrawProxy.sol`. This will cause the number of shares in `withdrawProxy.sol` to exceed the number of its own shares that `PublicVault.sol` owns. `processEpoch` will now try to burn more of its own shares than it has causing it to revert permanently. 

## Vulnerability Detail

    PublicVault.sol

    function redeem(
      uint256 shares,
      address receiver,
      address owner
    ) public virtual override returns (uint256 assets) {
      assets = redeemFutureEpoch(shares, receiver, owner, currentEpoch);
    }

    function redeemFutureEpoch(
      uint256 shares,
      address receiver,
      address owner,
      uint64 epoch
    ) public virtual returns (uint256 assets) {
      // check to ensure that the requested epoch is not the current epoch or in the past
      require(epoch >= currentEpoch, "Exit epoch too low");

      require(msg.sender == owner, "Only the owner can redeem");
      // check for rounding error since we round down in previewRedeem.

      ERC20(address(this)).safeTransferFrom(owner, address(this), shares);

      // Deploy WithdrawProxy if no WithdrawProxy exists for the specified epoch
      _deployWithdrawProxyIfNotDeployed(epoch);

      emit Withdraw(msg.sender, receiver, owner, assets, shares);

      // WithdrawProxy shares are minted 1:1 with PublicVault shares
      WithdrawProxy(withdrawProxies[epoch]).mint(receiver, shares); // was withdrawProxies[withdrawEpoch]
    }

When an LP wishes to redeem their shares, `redeemFutureEpoch` issues shares 1:1 to the `withdrawProxy.sol`

    ERC4626-Cloned.sol

    function deposit(uint256 assets, address receiver)
      public
      virtual
      override(IVault)
      returns (uint256 shares)
    {
      // Check for rounding error since we round down in previewDeposit.
      require((shares = previewDeposit(assets)) != 0, "ZERO_SHARES");

      // Need to transfer before minting or ERC777s could reenter.
      ERC20(underlying()).safeTransferFrom(msg.sender, address(this), assets);

      _mint(receiver, shares);

      emit Deposit(msg.sender, receiver, assets, shares);

      afterDeposit(assets, shares);
    }

Since withdrawProxy.sol inherits from `ERC4626-Cloned.sol` it is still possible to call `ERC4626-Cloned.sol#deposit`. This allows the supply of shares in the withdrawProxy to be greater than the number it's own shares that `PublicVault.sol` owns.

      uint256 proxySupply = WithdrawProxy(withdrawProxies[currentEpoch])
        .totalSupply();
      ...
      // burn the tokens of the LPs withdrawing
      _burn(address(this), proxySupply);

When `PublicVault.sol#processEpoch` is called it attempt to burn shares equal to the total supply of the proxy. As show above we can inflate the proxy supply to be higher than the number of shares taken during redeem. Inside `_burn` will underflow when `PublicVault.sol` tries to burn more shares than it has. We can walk through an example:

Assume `PublicVault.sol` has 1000 shares. An LP wishes to withdraw so they call redeem with 200 shares. This transfers 200 `PublicVault.sol` shares to `PublicVault.sol` and mints 200 `withdrawProxy.sol` shares to the LP. An attacker calls `ERC4626-Cloned.sol#deposit` to mint 1 share of `withdrawProxy.sol` to themselves. Now there are 201 shares of `withdrawProxy.sol` and only 200 `PublicVault.sol` shares in `PublicVault.sol`. When calling `PublicVault.sol#processEpoch` it will attempt to burn 201 `PublicVault.sol` shares which will revert since it only has 200.

## Impact

PublicVault.sol#processEpoch is permanently broken

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/PublicVault.sol#L239-L293

## Tool used

Manual Review

## Recommendation

Instead of burning the totalSupply of the withdrawProxy.sol, it should instead burn all the shares it currently owns.