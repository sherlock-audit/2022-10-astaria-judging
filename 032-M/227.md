hansfriese

medium

# ChangeInSlope function is wrong.

## Summary

`changeInSlope` function is wrong.

## Vulnerability Detail

In the `changeInSlope` function, the writer is getting the change in the slope assuming the new amount repaying.
But the comment is wrong because he is calculating the end debt as `rate*time*amount` while it should be `(1+rate*time)*amount`.
Furthermore, in the implementation it is not considered that the rate is in units of wad/second.

```solidity
  // LienToken.sol #462
  // slope = (rate*time*amount - amount) / time -> amount(rate*time - 1) / time
  uint256 newSlope = newAmount.mulDivDown(
    (uint256(lien.rate).mulDivDown(lien.duration, 1) - 1),
    lien.duration
  );
```

Below is a test contract.

```solidity
pragma solidity ^0.8.17;

import "forge-std/Test.sol";

import {MockERC721} from "solmate/test/utils/mocks/MockERC721.sol";
import {
  MultiRolesAuthority
} from "solmate/auth/authorities/MultiRolesAuthority.sol";

import {AuctionHouse} from "gpl/AuctionHouse.sol";
import {ERC721} from "gpl/ERC721.sol";
import {IAuctionHouse} from "gpl/interfaces/IAuctionHouse.sol";

import {IAstariaRouter, AstariaRouter} from "../AstariaRouter.sol";
import {IVault, VaultImplementation} from "../VaultImplementation.sol";
import {LiquidationAccountant} from "../LiquidationAccountant.sol";
import {PublicVault} from "../PublicVault.sol";
import {TransferProxy} from "../TransferProxy.sol";
import {WithdrawProxy} from "../WithdrawProxy.sol";

import "./TestHelpers.t.sol";

contract AstariaTest is TestHelpers {
  using FixedPointMathLib for uint256;
  using CollateralLookup for address;
  using SafeCastLib for uint256;

  function testChangeInSlope() public {
    TestNFT nft = new TestNFT(1);
    address tokenContract = address(nft);
    uint256 tokenId = uint256(0);
    uint256 initialBalance = WETH9.balanceOf(address(this));

    // create a PublicVault with a 14-day epoch
    address publicVault = _createPublicVault({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    // lend 50 ether to the PublicVault as address(1)
    _lendToVault(
      Lender({addr: address(1), amountToLend: 50 ether}),
      publicVault
    );

    // borrow 10 eth against the dummy NFT
    _commitToLien({
      vault: publicVault,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: 5 ether,
      isFirstLien: true
    });

    // check the status
    uint256 collateralTokenId = tokenContract.computeId(tokenId);
    emit log_named_decimal_uint("Current Debt", LIEN_TOKEN.getTotalDebtForCollateralToken(collateralTokenId), 18);
    uint256 lienId = LIEN_TOKEN.liens(collateralTokenId, 0);
    uint256 slope = LIEN_TOKEN.calculateSlope(lienId);
    emit log_named_decimal_uint("Slope * 365 days", slope * 365 days, 18);
    uint256 slopeChange = LIEN_TOKEN.changeInSlope(lienId, 1 ether);
    emit log_named_decimal_uint("ChangeInSlope * 365 days", slopeChange * 365 days, 18);
  }
}
```

When we run the above with the current implementation the result is as follows.

```solidity
> forge test --ffi -vv  --match-test testChangeInSlope
Failing tests:
Encountered 1 failing test in src/test/auditChangeInSlope.sol:AstariaTest
[FAIL. Reason: Arithmetic over/underflow] testChangeInSlope() (gas: 1993963)
```

Below is a result of the same test contract after fix.

```solidity
> forge test --ffi -vv  --match-test testChangeInSlope
[PASS] testChangeInSlope() (gas: 1936572)
Logs:
  Current Debt: 5.000000000000000000
  Slope * 365 days: 7.499999999898000000
  ChangeInSlope * 365 days: 1.499999999979600000
```

## Impact

For now this function is not used but this can lead to a serious problem in the future once it's used because it's related to the interest calcluation of the vault.

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L463

## Tool used

Manual Review

## Recommendation

Fix the `changeInSlope` function as below.

```solidity
function changeInSlope(uint256 lienId, uint256 paymentAmount)
  public
  view
  returns (uint256 slope)
{
  Lien memory lien = lienData[lienId];
  uint256 oldSlope = calculateSlope(lienId);
  uint256 newAmount = (lien.amount - paymentAmount);

  // slope = ((rate*time+1)*amount - amount) / time = amount*rate
  uint256 newSlope = newAmount.mulDivDown(lien.rate, INTEREST_DENOMINATOR); //@audit fixed
  slope = oldSlope - newSlope;
}

```