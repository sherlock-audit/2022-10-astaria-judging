hansfriese

high

# Rate is used in wrong units

## Summary

Through the whole protocol, rate is assumed to be in units of wad/seconds. And it is being used as if it is a decimal ranging in (0, 1) in many places.

## Vulnerability Detail

The rate of `LienDetails` is in units of wad/seconds. We can see that the max rate is set to `200 * 1e16 / 365 days = 63419583966`(AstariaRouter.sol#L109) by default. And this is used for comparison of new `LienDetails` rate at VaultImplementation.sol#L202.

But this rate is being used incorrectly in several places.

```solidity
// VaultImplementation.sol#L221
uint256 potentialDebt = seniorDebt * (ld.rate + 1) * ld.duration;
// LienToken.sol#L256
uint256 potentialDebt = totalDebt *
      (impliedRate + 1) *
      params.terms.duration;
```

Obviously this will give a very big result because it's being used without standardization and the user will never be able to borrow more than once (for the first loan it will be fine due to the previous debt is zero).

Below is a test contract showing this.

```solidity
pragma solidity ^0.8.17;

import "forge-std/Test.sol";

import {MockERC721} from "solmate/test/utils/mocks/MockERC721.sol";
import {
  MultiRolesAuthority
} from "solmate/auth/authorities/MultiRolesAuthority.sol";

import {AuctionHouse} from "gpl/AuctionHouse.sol";
import {ERC721} from "gpl/ERC721.sol";
import {IAuctionHouse} from "gpl/interfaces/IAuctionHouse.sol";

import {IAstariaRouter, AstariaRouter} from "../AstariaRouter.sol";
import {IVault, VaultImplementation} from "../VaultImplementation.sol";
import {LiquidationAccountant} from "../LiquidationAccountant.sol";
import {PublicVault} from "../PublicVault.sol";
import {TransferProxy} from "../TransferProxy.sol";
import {WithdrawProxy} from "../WithdrawProxy.sol";

import "./TestHelpers.t.sol";

contract AstariaTest is TestHelpers {
  using FixedPointMathLib for uint256;
  using CollateralLookup for address;
  using SafeCastLib for uint256;

  function testWrongRateUnit() public {
    // Dummy721 nft = new Dummy721();
    TestNFT nft = new TestNFT(1);
    address tokenContract = address(nft);
    uint256 tokenId = uint256(0);

    uint256 initialBalance = WETH9.balanceOf(address(this));

    // create a PublicVault with a 14-day epoch
    address publicVault = _createPublicVault({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    // lend 50 ether to the PublicVault as address(1)
    _lendToVault(
      Lender({addr: address(1), amountToLend: 50 ether}),
      publicVault
    );

    // borrow 50 eth against the dummy NFT, but split into several times
    _commitToLien({
      vault: publicVault,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: 1 ether,
      isFirstLien: true
    });
    // below will fail
    _commitToLien({
      vault: publicVault,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: 1 ether,
      isFirstLien: false
    });
  }
}
```

The test result is as below, the implied rate is being decreased as time goes.

```solidity
> forge test --ffi -vv --match-test testWrongRateUnit
Failing tests:
Encountered 1 failing test in src/test/auditWrongRate.sol:AstariaTest
[FAIL. Reason: Vault._validateCommitment(): Attempting to initiate a loan with debt potentially higher than maxPotentialDebt] testWrongRateUnit() (gas: 2185671)
```

## Impact

A borrower can never borrow more than once for a collateral.

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L221
https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L256

## Tool used

Foundry

## Recommendation

Fix the parts that a rate is used as below.

```solidity
// VaultImplementation.sol#L221
// uint256 potentialDebt = seniorDebt * (ld.rate + 1) * ld.duration;
uint256 potentialDebt = seniorDebt * (ld.rate * ld.duration + INTEREST_DENOMINATOR) / INTEREST_DENOMINATOR;
// LienToken.sol#L256
// uint256 potentialDebt = totalDebt *
//      (impliedRate + 1) *
//      params.terms.duration;
 uint256 potentialDebt = totalDebt * (impliedRate * params.terms.duration + INTEREST_DENOMINATOR) / INTEREST_DENOMINATOR

```