TurnipBoy

high

# LiquidationAccountant.sol#claim is an unprotected function and can lead to a stale withdrawRatio used to distribute funds

## Summary

`LiquidationAccountant.sol#claim` is an unprotected function that can be called at soon as the current timestamp is > `finalAuctionEnd`. It is not required to wait for the update `withdrawRatio` from `PublicVault.sol` before being called. This could be used by an attacker to gain a much larger share of profits. Since the default value of `withdrawRatio` is 0, an attacker can frontrun `PublicVault.sol#processEpoch` and distribute all underlying to the vault and none the withdrawing LPs. The withdrawing LPs would receive nothing and `PublicVault.sol` would receive all of their underlying.

## Vulnerability Detail

    function claim() public {
      require(
        block.timestamp > finalAuctionEnd || finalAuctionEnd == uint256(0),
        "final auction has not ended"
      );

      uint256 balance = ERC20(underlying()).balanceOf(address(this));
      // would happen if there was no WithdrawProxy for current epoch
      if (withdrawRatio == uint256(0)) {
        ERC20(underlying()).safeTransfer(VAULT(), balance);
      } else {
        uint256 transferAmount = withdrawRatio.mulDivDown(balance, 1e18);

        if (transferAmount > uint256(0)) {
          ERC20(underlying()).safeTransfer(WITHDRAW_PROXY(), transferAmount);
        }

        unchecked {
          balance -= transferAmount;
        }

        ERC20(underlying()).safeTransfer(VAULT(), balance);
      }

      PublicVault(VAULT()).decreaseYIntercept(
        (expected - ERC20(underlying()).balanceOf(address(this))).mulDivDown(
          1e18 - withdrawRatio,
          1e18
        )
      );
    }

`LiquidationAccountant.sol#claim` is an unpermissioned function that can be called by anyone as soon as the finalAuction ends. It will distribute the underlying at whatever withdrawRatio is currently has stored. 

    function processEpoch() external {
      // check to make sure epoch is over
      require(getEpochEnd(currentEpoch) < block.timestamp, "Epoch has not ended");
      require(withdrawReserve == 0, "Withdraw reserve not empty");
      if (liquidationAccountants[currentEpoch] != address(0)) {
        require(
          LiquidationAccountant(liquidationAccountants[currentEpoch])
            .getFinalAuctionEnd() < block.timestamp,
          "Final auction not ended"
        );
      }

    // split funds from LiquidationAccountant between PublicVault and WithdrawProxy if hasn't been already
    if (
      currentEpoch != 0 &&
      liquidationAccountants[currentEpoch - 1] != address(0)
    ) {
      LiquidationAccountant(liquidationAccountants[currentEpoch - 1]).claim();
    }

    require(
      liensOpenForEpoch[currentEpoch] == uint256(0),
      "loans are still open for this epoch"
    );

    // reset liquidationWithdrawRatio to prepare for re calcualtion
    liquidationWithdrawRatio = 0;

    // check if there are LPs withdrawing this epoch
    if (withdrawProxies[currentEpoch] != address(0)) {
      uint256 proxySupply = WithdrawProxy(withdrawProxies[currentEpoch])
        .totalSupply();

      liquidationWithdrawRatio = proxySupply.mulDivDown(1e18, totalSupply());

      if (liquidationAccountants[currentEpoch] != address(0)) {
        LiquidationAccountant(liquidationAccountants[currentEpoch])
          .setWithdrawRatio(liquidationWithdrawRatio);
      }

      uint256 withdrawAssets = convertToAssets(proxySupply);
      // compute the withdrawReserve
      uint256 withdrawLiquidations = liquidationsExpectedAtBoundary[
        currentEpoch
      ].mulDivDown(liquidationWithdrawRatio, 1e18);
      withdrawReserve = withdrawAssets - withdrawLiquidations;
      // burn the tokens of the LPs withdrawing
      _burn(address(this), proxySupply);

      _decreaseYIntercept(withdrawAssets);
    }

Normally the `withdrawRatio` is update during `PublicVault.sol#processEpoch` when it calls `LiquidationAccountant(liquidationAccountants[currentEpoch]).setWithdrawRatio(liquidationWithdrawRatio)`. An attacker can call `LiquidationAccountant.sol#claim` before `PublicVault.sol` updates it's `withdrawRatio` to send the wrong splits.

## Impact

Withdrawing LPs have all their underlying stolen

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LiquidationAccountant.sol#L65-L97

## Tool used

Manual Review

## Recommendation

`LiquidationAccountant.sol#claim` should only be callable by `PublicVault.sol`. Ideally `PublicVault.sol#processEpoch` should call it immediately after setting the withdraw ratio.
