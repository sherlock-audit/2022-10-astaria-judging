rvierdiiev

high

# ASTARIA_ROUTER.getBuyoutFee will always revert

## Summary
`ASTARIA_ROUTER.getBuyoutFee ` will always revert, as `buyoutFeeNumerator` and `buyoutFeeDenominator` variables are not initialized with any values.
## Vulnerability Detail
`ASTARIA_ROUTER.buyoutFeeNumerator` and `ASTARIA_ROUTER.buyoutFeeDenominator` variables are not initialized anywhere in the contract and there is no ability to set them through the code. That leads to the fact that `ASTARIA_ROUTER.getBuyoutFee ` function will always revert because of divide by 0.

`ASTARIA_ROUTER.getBuyoutFee ` function is used by `LienToken.getBuyout`.

```solidity
  function getBuyout(uint256 collateralId, uint256 position)
    public
    view
    returns (uint256, uint256)
  {
    Lien memory lien = getLien(collateralId, position);

    uint256 remainingInterest = _getRemainingInterest(lien, true);
    uint256 buyoutTotal = lien.amount +
      ASTARIA_ROUTER.getBuyoutFee(remainingInterest);

    return (lien.amount, buyoutTotal);
  }
```

As a result `LienToken.getBuyout` will always revert. The same [will be](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L132) with `LienToken.buyoutLien` function. That means that buyout is not working currently.
## Impact
Buyout is not possible.
## Code Snippet
Just add this to `AstariaTest`.
```solidity
function testFeeDdos() public {
    TestNFT nft = new TestNFT(1);
    address tokenContract = address(nft);
    uint256 tokenId = uint256(0);

    // create a PublicVault with a 14-day epoch
    address publicVault = _createPublicVault({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    // lend 50 ether to the PublicVault as address(1)
    _lendToVault(
      Lender({addr: address(1), amountToLend: 50 ether}),
      publicVault
    );

    // borrow 10 eth against the dummy NFT
    _commitToLien({
      vault: publicVault,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: 10 ether,
      isFirstLien: true
    });

    uint256 collateralId = tokenContract.computeId(tokenId);

    vm.expectRevert();
    LIEN_TOKEN.getBuyout(collateralId, 0);
  }
```
## Tool used

Manual Review

## Recommendation
Add ability to set `ASTARIA_ROUTER.buyoutFeeNumerator` and `ASTARIA_ROUTER.buyoutFeeDenominator` variables and validate input.