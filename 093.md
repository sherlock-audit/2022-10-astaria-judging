zzykxx

high

# Users calling `makePayment()` for collaterals with multiple liens might lose funds

## Summary

Users might lose funds when trying to payback a loan, if the collateral has been used for more than one loan.

## Vulnerability Detail

The function `function _payment(uint256 collateralId, uint8 position, uint256 paymentAmount, address payer)` at [LienToken.sol#L594](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L594), which is called in a loop by `makePayment`, doesn't take into account the possible difference between `paymentAmount` and the actual amount to be paid, `lien.amount`, instead it just takes it all. This can lead to a loss of funds by a borrower who logically expects the call to pay for all the liens.

## Impact

When an user calls `makePayment(uint256 collateralId, uint256 paymentAmount)` at [LienToken.sol#L387](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L387) and has multiple liens for the same NFT, only the first lien is paid back, no matter how big the amount. This causes the user to lose funds, in favour of the strategist and liquidity providers.

## Code Snippet

Just copy-paste in `AstariaTest.t.sol` and run with `--via-ir` if necessary:

```solidity
  function testMakePaymentForMultipleLiens() public {
    // Dummy721 nft = new Dummy721();
    TestNFT nft = new TestNFT(1);
    address tokenContract = address(nft);
    uint256 tokenId = uint256(0);

    uint256 initialBalance = WETH9.balanceOf(address(this));

    /*

        Two vaults that accepr TestNFT are available with liquidity

    */

    address publicVault = _createPublicVault({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    address publicVault2 = _createPublicVault({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    _lendToVault(
      Lender({addr: address(1), amountToLend: 50 ether}),
      publicVault
    );

    _lendToVault(
      Lender({addr: address(1), amountToLend: 50 ether}),
      publicVault2
    );

    /*

        ALICE takes two loans with TestNFT

    */

    uint256 firstLoanAmount = 100;
    uint256 secondLoanAmount = 10;
    // borrow 10 eth against the dummy NFT
    _commitToLien({
      vault: publicVault,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: firstLoanAmount,
      isFirstLien: true
    });

    _commitToLien({
      vault: publicVault2,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: secondLoanAmount,
      isFirstLien: false
    });

    /*
      
      ALICE repays her 110 wei debt with 150 wei

    */

    uint256 repayAmount = 150;

    uint256 collateralId = tokenContract.computeId(tokenId);
    assertEq(WETH9.balanceOf(address(this)), initialBalance + firstLoanAmount + secondLoanAmount);
    uint256 amount = firstLoanAmount + secondLoanAmount;
    vm.deal(address(this), repayAmount);
    WETH9.deposit{value: repayAmount}();
    WETH9.approve(address(TRANSFER_PROXY), repayAmount);
    WETH9.approve(address(LIEN_TOKEN), repayAmount);
    LIEN_TOKEN.makePayment(collateralId, repayAmount);

    /*
      
      ALICE tries to release her NFT but she can't because all of repayAmount
      has been considered as a payment for the first loan

    */

    vm.expectRevert("must be no liens or auctions to call this");
    COLLATERAL_TOKEN.releaseToAddress(collateralId, address(this));
  }
```

## Tool used

Manual Review

## Recommendation

In `_payment` at [LienToken.sol#L737](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L737) make sure the amount transferred is at maximum `lien.amount`.
