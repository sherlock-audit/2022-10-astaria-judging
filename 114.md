ctf_sec

high

# AuctionHouse#createBid bid refund logic is wrong when new bid overwrites a existing bid.

## Summary

AuctionHouse#createBid bid refund logic is wrong when a new bid overwrites an existing bid.

The new bidder is paying the refund for the old bidder, instead of the current auction contract refund the old bid.

## Vulnerability Detail

The function AuctionHouse.sol#createBid is implemented below:

./lib/astaria-gpl/src/AuctionHouse.sol

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/AuctionHouse.sol#L94

```solidity
  function createBid(uint256 tokenId, uint256 amount) external override {
    address lastBidder = auctions[tokenId].bidder;
    uint256 currentBid = auctions[tokenId].currentBid;
    uint256 duration = auctions[tokenId].duration;
    uint64 firstBidTime = auctions[tokenId].firstBidTime;
    require(
      firstBidTime == 0 || block.timestamp < firstBidTime + duration,
      "Auction expired"
    );
    require(
      amount > currentBid + ((currentBid * minBidIncrementPercentage) / 100),
      "Must send more than last bid by minBidIncrementPercentage amount"
    );

    // If this is the first valid bid, we should set the starting time now.
    // If it's not, then we should refund the last bidder
    uint256 vaultPayment = (amount - currentBid);

    if (firstBidTime == 0) {
      auctions[tokenId].firstBidTime = block.timestamp.safeCastTo64();
    } else if (lastBidder != address(0)) {
      uint256 lastBidderRefund = amount - vaultPayment;
      _handleOutGoingPayment(lastBidder, lastBidderRefund);
    }

    _handleIncomingPayment(tokenId, vaultPayment, address(msg.sender));

    auctions[tokenId].currentBid = amount;
    auctions[tokenId].bidder = address(msg.sender);

    bool extended = false;
    // at this point we know that the timestamp is less than start + duration (since the auction would be over, otherwise)
    // we want to know by how much the timestamp is less than start + duration
    // if the difference is less than the timeBuffer, increase the duration by the timeBuffer
    if (firstBidTime + duration - block.timestamp < timeBuffer) {
      // Playing code golf for gas optimization:
      // uint256 expectedEnd = auctions[auctionId].firstBidTime.add(auctions[auctionId].duration);
      // uint256 timeRemaining = expectedEnd.sub(block.timestamp);
      // uint256 timeToAdd = timeBuffer.sub(timeRemaining);
      // uint256 newDuration = auctions[auctionId].duration.add(timeToAdd);

      //TODO: add the cap to the duration, do not let it extend beyond 24 hours extra from max duration
      uint64 newDuration = uint256(
        duration + (block.timestamp + timeBuffer - firstBidTime)
      ).safeCastTo64();
      if (newDuration <= auctions[tokenId].maxDuration) {
        auctions[tokenId].duration = newDuration;
      } else {
        auctions[tokenId].duration =
          auctions[tokenId].maxDuration -
          firstBidTime;
      }
      extended = true;
    }

    emit AuctionBid(
      tokenId,
      msg.sender,
      amount,
      lastBidder == address(0), // firstBid boolean
      extended
    );

    if (extended) {
      emit AuctionDurationExtended(tokenId, auctions[tokenId].duration);
    }
  }
```

Our focus is in the codeblock below

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/AuctionHouse.sol#L112

```solidity
    if (firstBidTime == 0) {
      auctions[tokenId].firstBidTime = block.timestamp.safeCastTo64();
    } else if (lastBidder != address(0)) {
      uint256 lastBidderRefund = amount - vaultPayment;
      _handleOutGoingPayment(lastBidder, lastBidderRefund);
    }
```

What the code doing is that:

if the firstBidTime is 0, we set the current firsBidTime, otherwise, we refund the fund to the old bidder.

however, the crucial function _handleOutGoingPayment(lastBidder, lastBidderRefund) is implemented below

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/AuctionHouse.sol#L307

```solidity
  function _handleOutGoingPayment(address to, uint256 amount) internal {
    TRANSFER_PROXY.tokenTransferFrom(weth, address(msg.sender), to, amount);
  }
```

and the implement for TRANSFER_PROXY#tokenTransferFrom is 

```solidity
  function tokenTransferFrom(
    address token,
    address from,
    address to,
    uint256 amount
  ) external requiresAuth {
    ERC20(token).safeTransferFrom(from, to, amount);
  }
```

then the _handleOutGoingPayment transfer "amount" of WETH from msg.sender to the address(to).

now we revisit the old bid refunding block:

```solidity
    if (firstBidTime == 0) {
      auctions[tokenId].firstBidTime = block.timestamp.safeCastTo64();
    } else if (lastBidder != address(0)) {
      uint256 lastBidderRefund = amount - vaultPayment;
      _handleOutGoingPayment(lastBidder, lastBidderRefund);
    }
```

if the lastBidder exits, we are letting the current new bidder who is willing to pay the higher price and wants to overwrite the current bid paying the refund for the old bidder because of this line of code:

```solidity
TRANSFER_PROXY.tokenTransferFrom(weth, address(msg.sender), to, amount);
```

msg.sender is the current bidder, address(to) is the old bidder.

I believe this logic is not correctly implemented.

The new bidder should not refund the fund to the old bidder, whoever receives the old bidder's fund should refund the old bidder.

In this case, the old bidder's fund is used to either pay for LIEN_TOKEN or transferred to COLLATERAL_TOKEN.ownerOf(tokenId) in _handleIncomingPayment

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/AuctionHouse.sol#L254

Given the code bock

```solidity
    if (liens.length > 0) {
      for (uint256 i = 0; i < liens.length; ++i) {
        uint256 payment;
        uint256 lienId = liens[i];

        ILienToken.Lien memory lien = LIEN_TOKEN.getLien(lienId);

        if (transferAmount >= lien.amount) {
          payment = lien.amount;
          transferAmount -= payment;
        } else {
          payment = transferAmount;
          transferAmount = 0;
        }

        if (payment > 0) {
          LIEN_TOKEN.makePayment(tokenId, payment, lien.position, payer);
        }
      }
    } else {
      TRANSFER_PROXY.tokenTransferFrom(
        weth,
        payer,
        COLLATERAL_TOKEN.ownerOf(tokenId),
        transferAmount
      );
    }
```

## Impact

The new bidder is wrongly paying the refund for the old bidder and lose money unexpectedly.

## Code Snippet

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/AuctionHouse.sol#L307

https://github.com/AstariaXYZ/astaria-gpl/blob/0e6764f626b704fea23b5ba4c46afd963de298ca/src/AuctionHouse.sol#L254

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/TransferProxy.sol#L25-L32

## Tool used

Manual Review

## Recommendation

We recommend the project refund the old bidder's fund from  LIEN_TOKEN debt position or from the COLLATERAL_TOKEN.ownerOf(tokenId) address instead of letting the new bidder paying the refund.
