neila

medium

# Use _safeMint instead of _mint

## Summary
Use _safeMint instead of _mint
found by [Tomosuke0930](https://github.com/Tomosuke0930), [yawn-c111](https://github.com/yawn-c111), 

## Vulnerability Detail
The `VaultImplementation(params.vault).recipient()` will receive the LIEN_TOKEN as NFT when `createLien` is called.
However, if `VaultImplementation(params.vault).recipient()` is a contract address that does not support ERC721, the LIEN_TOKEN can be frozen in the contract.

## Impact
LIEN_TOKEN becomes frozen when recipient is a contract and does not support ERC721

https://github.com/unchain-dev/2022-10-astaria-UNCHAIN/blob/main/src/LienToken.sol#L235
```solidity

function createLien(ILienBase.LienActionEncumber memory params)
    external
    requiresAuth
    returns (uint256 lienId)
  {
    // require that the auction is not under way

    uint256 collateralId = params.tokenContract.computeId(params.tokenId);

    if (AUCTION_HOUSE.auctionExists(collateralId)) {
      revert InvalidCollateralState(InvalidStates.AUCTION);
    }

    (address tokenContract, ) = COLLATERAL_TOKEN.getUnderlying(collateralId);
    if (tokenContract == address(0)) {
      revert InvalidCollateralState(InvalidStates.NO_DEPOSIT);
    }

    uint256 totalDebt = getTotalDebtForCollateralToken(collateralId);
    uint256 impliedRate = getImpliedRate(collateralId);

    uint256 potentialDebt = totalDebt *
      (impliedRate + 1) *
      params.terms.duration;

    if (potentialDebt > params.terms.maxPotentialDebt) {
      revert InvalidCollateralState(InvalidStates.DEBT_LIMIT);
    }

    lienId = uint256(
      keccak256(
        abi.encodePacked(
          abi.encode(
            bytes32(collateralId),
            params.vault,
            WETH,
            params.terms.maxAmount,
            params.terms.rate,
            params.terms.duration,
            params.terms.maxPotentialDebt
          ),
          params.strategyRoot
        )
      )
    );

    //0 - 4 are valid
    require(
      uint256(liens[collateralId].length) < MAX_LIENS,
      "too many liens active"
    );

    uint8 newPosition = uint8(liens[collateralId].length);

    _mint(VaultImplementation(params.vault).recipient(), lienId);
    lienData[lienId] = Lien({
      collateralId: collateralId,
      position: newPosition,
      amount: params.amount,
      active: true,
      rate: params.terms.rate.safeCastTo240(),
      last: block.timestamp.safeCastTo32(),
      start: block.timestamp.safeCastTo32(),
      duration: params.terms.duration.safeCastTo32(),
      payee: address(0)
    });

    liens[collateralId].push(lienId);
    emit NewLien(lienId, lienData[lienId]);
  }

```

As per the comment of OpenZeppelin's ERC-721.sol:
> WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible

Ref: https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/ERC721.sol


## Code Snippet
`VaultImplementation(params.vault).recipient()` will call `recipient()`.
https://github.com/unchain-dev/2022-10-astaria-UNCHAIN/blob/main/src/VaultImplementation.sol#L310
```solidity
function recipient() public view returns (address) {
    if (VAULT_TYPE() == uint8(IAstariaRouter.VaultType.PUBLIC)) {
      return address(this);
    } else {
      return owner();
    }
  }
```

## Tool used
Manual Review

## Recommendation
Use `_safeMint` instead of `_mint`.
```solidity
/// before
_mint(VaultImplementation(params.vault).recipient(), lienId);

/// after
_safeMint(VaultImplementation(params.vault).recipient(), lienId);
```