TurnipBoy

high

# AstariaRouter.sol#lendToVault steals users' funds

## Summary

AstariaRouter.sol#lendToVault takes funds from caller and calls deposit on the target vault but neither `AstariaRouter.sol#lendToVault` nor `PublicVault.sol#deposit` credits the depositor with any shares or otherwise gives any indication of deposit.

## Vulnerability Detail

AstariaRouter.sol
    
    function lendToVault(IVault vault, uint256 amount) external whenNotPaused {
      TRANSFER_PROXY.tokenTransferFrom(
        address(WETH),
        address(msg.sender),
        address(this),
        amount
      );

      require(
        vaults[address(vault)] != address(0),
        "lendToVault: vault doesn't exist"
      );
      WETH.safeApprove(address(vault), amount);
      vault.deposit(amount, address(msg.sender));
    }

PublicVault.sol - PublicVault

    function deposit(uint256 amount, address receiver)
      public
      override(Vault, ERC4626Cloned)
      whenNotPaused
      returns (uint256)
    {
      return super.deposit(amount, receiver);
    }

PublicVault.sol - Vault

    function deposit(uint256 amount, address)
      public
      virtual
      override
      returns (uint256)
    {
      require(msg.sender == owner(), "only the appraiser can fund this vault");
      ERC20(underlying()).safeTransferFrom(
        address(msg.sender),
        address(this),
        amount
      );
      return amount;
    }

`AstariaRouter.sol#lendToVault` transfers funds from `msg.sender` to the `AstariaRouter.sol` contract. It then calls `deposit` on the target `PublicVault`. `PublicVault` implements a `deposit` that just calls `super.deposit`. `PublicVault` inherits from `Vault` which means that the `deposit` code shown directly above is called. No where in the flow is the depositor credited with any shares or funds, meaning their funds have effectively been stolen.

## Impact

Deposited funds are unrecoverable and don't credit anything to the depositor

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/AstariaRouter.sol#L324-L338

## Tool used

Manual Review

## Recommendation

`AstariaRouter.sol#lendToVault` or `PublicVault.sol#deposit` should credit the users with shares for their deposit.