ctf_sec

high

# VaultImplementation#_validateCommitment signature commit proof message can be reused / replayed because the lack of nonce check

## Summary

VaultImplementation#_validateCommitment signature commit proof message can be reused / replayed because the lack of nonce check

## Vulnerability Detail

VaultImplementation#_validateCommitment signature commit proof message can be reused / replayed because the lack of nonce check

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L135-L186

as we can see, the recovered address is validated against the strategist and owner and delegate

```solidity
    address recovered = ecrecover(
      keccak256(
        encodeStrategyData(
          params.lienRequest.strategy,
          params.lienRequest.merkle.root
        )
      ),
      params.lienRequest.v,
      params.lienRequest.r,
      params.lienRequest.s
    );
    require(
      recovered == params.lienRequest.strategy.strategist,
      "strategist must match signature"
    );
    require(
      recovered == owner() || recovered == delegate,
      "invalid strategist"
    );
```

however, this implementation is vulnerable to signature reuse / replay attack.

We need to check encodeStrategyData

```solidity
  // cast k "StrategyDetails(uint256 nonce,uint256 deadline,bytes32 root)"
  bytes32 private constant STRATEGY_TYPEHASH =
    0x679f3933bd13bd2e4ec6e9cde341ede07736ad7b635428a8a211e9cccb4393b0;

  function encodeStrategyData(
    IAstariaRouter.StrategyDetails calldata strategy,
    bytes32 root
  ) public view returns (bytes memory) {
    bytes32 hash = keccak256(
      abi.encode(
        STRATEGY_TYPEHASH,
        IAstariaRouter(ROUTER()).strategistNonce(strategy.strategist),
        strategy.deadline,
        root
      )
    );
    return
      abi.encodePacked(bytes1(0x19), bytes1(0x01), domainSeparator(), hash);
  }
```

We use IAstariaRouter(ROUTER()).strategistNonce(strategy.strategist), but we does not increment the nonce each time after the commitment message is used.

## Impact

The impact is severe, malicious actors can reuse the commitment message to bypass the check _validateCommitment and execute commitToLien and mess up the life cycle of new loan origination.

VaultImplementation#commitToLien

```solidity
  /**
   * @notice Pipeline for lifecycle of new loan origination.
   * Origination consists of a few phases: pre-commitment validation, lien token issuance, strategist reward, and after commitment actions
   * Starts by depositing collateral and take out a lien against it. Next, verifies the merkle proof for a loan commitment. Vault owners are then rewarded fees for successful loan origination.
   * @param params Commitment data for the incoming lien request
   * @param receiver The borrower receiving the loan.
   */
  function commitToLien(
    IAstariaRouter.Commitment calldata params,
    address receiver
  ) external whenNotPaused {
    IAstariaRouter.LienDetails memory ld = _validateCommitment(
      params,
      receiver
    );
    uint256 lienId = _requestLienAndIssuePayout(ld, params, receiver);
    _afterCommitToLien(lienId, params.lienRequest.amount);
    emit NewLien(
      params.lienRequest.merkle.root,
      params.tokenContract,
      params.tokenId,
      params.lienRequest.amount
    );
  }
```

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L93-L108

## Tool used

Manual Review

## Recommendation

We recommend the projected increment the nonce each time after the commitment is used in

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L93-L108



