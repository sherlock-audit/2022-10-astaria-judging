hansfriese

high

# A borrower can claim a NFT back with a tiny bid at auction

## Summary

The house accepts all bids regardless of the amount and it releases the NFT to the winner when `endAuction` is called.
A borrower can waits until the auction starts and create a bid with a tiny amount and then call `endAuction` to claim the NFT back.

## Vulnerability Detail

The function `createBid` does not check the amount and process the incoming payment and remembers it as a last bidder.
In the function `endAuction` at AuctionHouse.sol#L166, the house removes the lien tokens for the collateral NFT and releases it to the 'winner' without checking if the winner bid amount is greater than the `reservePrice`.

```solidity
// AuctionHouse.sol#L93
function createBid(uint256 tokenId, uint256 amount) external override {
    address lastBidder = auctions[tokenId].bidder;
    uint256 currentBid = auctions[tokenId].currentBid;
    uint256 duration = auctions[tokenId].duration;
    uint64 firstBidTime = auctions[tokenId].firstBidTime;
    require(
      firstBidTime == 0 || block.timestamp < firstBidTime + duration,
      "Auction expired"
    );
    require(//@audit bids are accepted as long as the amount is over than the previous bid by some percentage, so tiny amount is accepted at first
      amount > currentBid + ((currentBid * minBidIncrementPercentage) / 100),
      "Must send more than last bid by minBidIncrementPercentage amount"
    );

    // If this is the first valid bid, we should set the starting time now.
    // If it's not, then we should refund the last bidder
    uint256 vaultPayment = (amount - currentBid);

    if (firstBidTime == 0) {
      auctions[tokenId].firstBidTime = block.timestamp.safeCastTo64();
    } else if (lastBidder != address(0)) {
      uint256 lastBidderRefund = amount - vaultPayment;
      _handleOutGoingPayment(lastBidder, lastBidderRefund);
    }

    _handleIncomingPayment(tokenId, vaultPayment, address(msg.sender));

    auctions[tokenId].currentBid = amount;
    auctions[tokenId].bidder = address(msg.sender);
  ...
}

// AuctionHouse.sol#L166
function endAuction(uint256 auctionId)
  external
  override
  requiresAuth
  returns (address winner)
{
  require(
    block.timestamp >=
      auctions[auctionId].firstBidTime + auctions[auctionId].duration,
    "Auction hasn't completed"
  );
  Auction storage auction = auctions[auctionId];
  if (auction.bidder == address(0)) {
    winner = auction.initiator;
  } else {
    winner = auction.bidder; //@audit winner is decided without checking the bid amount
  }

  emit AuctionEnded(auctionId, winner, auction.currentBid, auction.recipients);
  uint256[] memory liensRemaining = LIEN_TOKEN.getLiens(auctionId);

  for (uint256 i = 0; i < liensRemaining.length; i++) {
    ILienToken.Lien memory lien = LIEN_TOKEN.getLien(liensRemaining[i]);
    if (
      PublicVault(LIEN_TOKEN.ownerOf(i)).supportsInterface(
        type(IPublicVault).interfaceId
      )
    ) {
      PublicVault(LIEN_TOKEN.ownerOf(i)).decreaseYIntercept(lien.amount);
    }
  }
  LIEN_TOKEN.removeLiens(auctionId, liensRemaining); //@audit not checking if this auction received enough money to remove these lien tokens
  delete auctions[auctionId];
}

// CollateralToken.sol #341: a borrower can call endAuction
function endAuction(uint256 tokenId) external {
  require(AUCTION_HOUSE.auctionExists(tokenId), "Auction doesn't exist");

  address winner = AUCTION_HOUSE.endAuction(tokenId);
  _releaseToAddress(tokenId, winner);
}
```

## Impact

The house can lose an NFT at a price lower than the reserved amount.
A borrower can wait until his NFT goes to auction and bid a very small amount and then end the auction proactively to claim his NFT back.

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/CollateralToken.sol#L341
https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L166
https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/AuctionHouse.sol#L93

## Tool used

Manual review

## Recommendation

Check if the bid amount is not less than the reserved amount in the function `endAuction` as below.

```solidity
function endAuction(uint256 auctionId)
    external
    override
    requiresAuth
    returns (address winner)
  {
    require(
      auction.currentBid >= auction.reservePrice,
      "Current bid is not enough"
    );
    require(
      block.timestamp >=
        auctions[auctionId].firstBidTime + auctions[auctionId].duration,
      "Auction hasn't completed"
    );
    Auction storage auction = auctions[auctionId];
    if (auction.bidder == address(0)) {
      winner = auction.initiator;
    } else {
      winner = auction.bidder;
    }

    ...

```