hansfriese

high

# Implied rate calculation is wrong

## Summary

The function `getImpliedRate` at `LienToken.sol #511` is wrong.

## Vulnerability Detail

The function `getImpliedRate` returns a wrong rate.
I beleive the writer intended to return the weighted average of rates for all liens against a specific collateral.
But the denominator is calculated as the total debt accrued so far while the current lien amount is being used in the numerator.
As a result, the implied rate is calculated less than the correct one.
`impliedRate` is used when a new loan is created to check if the potential debt does not exceed the one specified in the loan terms.
So finally, with a less implied rate result, the borrower can lend more than the potential debt.

```solidity
function getImpliedRate(uint256 collateralId)
  public
  view
  returns (uint256 impliedRate)
{
  uint256 totalDebt = getTotalDebtForCollateralToken(collateralId);
  uint256[] memory openLiens = getLiens(collateralId);
  impliedRate = 0;
  for (uint256 i = 0; i < openLiens.length; ++i) {
    Lien memory lien = lienData[openLiens[i]];

    impliedRate += lien.rate * lien.amount; //@audit should use the debt of lien at the moment _getOwed(lien)
  }

  if (totalDebt > uint256(0)) {
    impliedRate = impliedRate.mulDivDown(1, totalDebt);
  }
}

```

Below is a test contract showing a wrong implied rate as time goes.

```solidity
pragma solidity ^0.8.17;

import "forge-std/Test.sol";

import {MockERC721} from "solmate/test/utils/mocks/MockERC721.sol";
import {
  MultiRolesAuthority
} from "solmate/auth/authorities/MultiRolesAuthority.sol";

import {AuctionHouse} from "gpl/AuctionHouse.sol";
import {ERC721} from "gpl/ERC721.sol";
import {IAuctionHouse} from "gpl/interfaces/IAuctionHouse.sol";

import {IAstariaRouter, AstariaRouter} from "../AstariaRouter.sol";
import {IVault, VaultImplementation} from "../VaultImplementation.sol";
import {LiquidationAccountant} from "../LiquidationAccountant.sol";
import {PublicVault} from "../PublicVault.sol";
import {TransferProxy} from "../TransferProxy.sol";
import {WithdrawProxy} from "../WithdrawProxy.sol";

import "./TestHelpers.t.sol";

contract AstariaTest is TestHelpers {
  using FixedPointMathLib for uint256;
  using CollateralLookup for address;
  using SafeCastLib for uint256;

  function testImpliedRate() public {
    TestNFT nft = new TestNFT(1);
    address tokenContract = address(nft);
    uint256 tokenId = uint256(0);
    uint256 initialBalance = WETH9.balanceOf(address(this));

    // create a PublicVault with a 14-day epoch
    address publicVault = _createPublicVault({
      strategist: strategistOne,
      delegate: strategistTwo,
      epochLength: 14 days
    });

    // lend 50 ether to the PublicVault as address(1)
    _lendToVault(
      Lender({addr: address(1), amountToLend: 50 ether}),
      publicVault
    );

    // borrow 10 eth against the dummy NFT
    _commitToLien({
      vault: publicVault,
      strategist: strategistOne,
      strategistPK: strategistOnePK,
      tokenContract: tokenContract,
      tokenId: tokenId,
      lienDetails: standardLien,
      amount: 5 ether,
      isFirstLien: true
    });

    // check the status
    uint256 collateralTokenId = tokenContract.computeId(tokenId);
    emit log_named_decimal_uint("Current Debt", LIEN_TOKEN.getTotalDebtForCollateralToken(collateralTokenId), 18);
    emit log_named_uint("Original Rate from LienDetails", standardLien.rate * 365 days / 1e16);
    emit log_named_uint("Implied Rate (beginning)", LIEN_TOKEN.getImpliedRate(collateralTokenId) * 365 days / 1e16);
    vm.warp(block.timestamp + 7 days);
    emit log_named_uint("Implied Rate (after 7 days)", LIEN_TOKEN.getImpliedRate(collateralTokenId) * 365 days / 1e16);
  }
}
```

The test result is as below, the implied rate is being decreased as time goes.

```solidity
> forge test --ffi -vv --match-test testImpliedRate
  Current Debt: 5.000000000000000000
  Original Rate from LienDetails: 149
  Implied Rate (beginning): 149
  Implied Rate (after 7 days): 145
```

## Impact

A borrower can borrow more than the `maxPotentialDebt` of the loan terms.

## Code Snippet

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L256

## Tool used

Manual Review

## Recommendation

Fix the `getImpliedRate` function as below.

```solidity
function getImpliedRate(uint256 collateralId)
  public
  view
  returns (uint256 impliedRate)
{
  uint256 totalDebt = getTotalDebtForCollateralToken(collateralId);
  uint256[] memory openLiens = getLiens(collateralId);
  impliedRate = 0;
  for (uint256 i = 0; i < openLiens.length; ++i) {
    Lien memory lien = lienData[openLiens[i]];

    impliedRate += lien.rate * _getOwed(lien); //@audit fixed here
  }

  if (totalDebt > uint256(0)) {
    impliedRate = impliedRate.mulDivDown(1, totalDebt);
  }
}

```