Jeiwan

high

# The implied value of a public vault can be impaired, liquidity providers can lose funds

## Summary
The implied value of a public vault can be impaired, liquidity providers can lose funds
## Vulnerability Detail
Borrowers can partially repay their liens, which is handled by the `_payment` function ([LienToken.sol#L594](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L594)).
When repaying a part of a lien, `lien.amount` is updated to include currently accrued debt ([LienToken.sol#L605-L617](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L605-L617)):
```solidity
Lien storage lien = lienData[lienId];
lien.amount = _getOwed(lien); // @audit current debt, including accrued interest; saved to storage!
```
Notice that `lien.amount` is updated in storage, and `lien.last` wasn't updated.

Then, lien's slope is subtracted from vault's slope accumulator to be re-calculated after the repayment ([LienToken.sol#L620-L630](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L620-L630)):
```solidity
if (isPublicVault) {
  // @audit calculates and subtracts lien's slope from vault's slope
  IPublicVault(lienOwner).beforePayment(lienId, paymentAmount);
}
if (lien.amount > paymentAmount) {
  lien.amount -= paymentAmount;
  // @audit lien.last is updated only after payment amount subtraction
  lien.last = block.timestamp.safeCastTo32();
  // slope does not need to be updated if paying off the rest, since we neutralize slope in beforePayment()
  if (isPublicVault) {
    // @audit re-calculates and re-applies lien's slope after the repayment
    IPublicVault(lienOwner).afterPayment(lienId);
  }
}
```

In the `beforePayment` function, `LIEN_TOKEN().calculateSlope(lienId)` is called to calculate lien's current slope ([PublicVault.sol#L433-L442](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/PublicVault.sol#L433-L442)):
```solidity
function beforePayment(uint256 lienId, uint256 amount) public onlyLienToken {
  _handleStrategistInterestReward(lienId, amount);
  uint256 lienSlope = LIEN_TOKEN().calculateSlope(lienId);
  if (lienSlope > slope) {
    slope = 0;
  } else {
    slope -= lienSlope;
  }
  last = block.timestamp;
}
```

The `calculateSlope` function reads a lien from storage and calls `_getOwed` again ([LienToken.sol#L440-L445](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L440-L445)):
```solidity
function calculateSlope(uint256 lienId) public view returns (uint256) {
  // @audit lien.amount includes interest accrued so far
  Lien memory lien = lienData[lienId];
  uint256 end = (lien.start + lien.duration);
  uint256 owedAtEnd = _getOwed(lien, end);
  // @audit lien.last wasn't updated in `_payment`, it's an older timestamp
  return (owedAtEnd - lien.amount).mulDivDown(1, end - lien.last);
}
```

This is where double counting of accrued interest happens. Recall that lien's amount already includes the interest that was accrued by this moment (in the `_payment` function). Now, interest is calculated again and *is applied to the amount that already includes (a portion) it* ([LienToken.sol#L544-L550](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L544-L550)):
```solidity
function _getOwed(Lien memory lien, uint256 timestamp)
  internal
  view
  returns (uint256)
{
  // @audit lien.amount already includes interest accrued so far
  return lien.amount + _getInterest(lien, timestamp);
}
```

[LienToken.sol#L177-L196](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L177-L196):
```solidity
function _getInterest(Lien memory lien, uint256 timestamp)
  internal
  view
  returns (uint256)
{
  if (!lien.active) {
    return uint256(0);
  }
  uint256 delta_t;
  if (block.timestamp >= lien.start + lien.duration) {
    delta_t = uint256(lien.start + lien.duration - lien.last);
  } else {
    // @audit lien.last wasn't updated in `_payment`, so the `delta_t` is bigger here
    delta_t = uint256(timestamp.safeCastTo32() - lien.last);
  }
  return
    // @audit rate applied to a longer delta_t and multiplied by a bigger amount than expected
    delta_t.mulDivDown(lien.rate, 1).mulDivDown(
      lien.amount,
      INTEREST_DENOMINATOR
    );
}
```
## Impact
Double counting of interest will result in a wrong lien slope, which will affect the vault's slope accumulator.  This will result in an invalid implied value of a vault ([PublicVault.sol#L406-L413](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/PublicVault.sol#L406-L413)):
1. If miscalculated lien slope is bigger than expected, vault's slope will be smaller than expected (due to the subtraction in `beforePayment`), and vault's implied value will also be smaller. Liquidity providers will lose money because they won't be able to redeem the whole liquidity (vault's implied value, `totalAssets`, is used in the conversion of LP shares, [ERC4626-Cloned.sol#L392-L412](https://github.com/sherlock-audit/2022-10-astaria/blob/main/lib/astaria-gpl/src/ERC4626-Cloned.sol#L392-L412))
1. If miscalculated lien slope is smaller than expected, vault's slope will be higher, and vaults implied value will also be higher. However, it won't be backed by actual liquidity, thus the liquidity providers that exit earlier will get a bigger share of the underlying assets. The last liquidity provider won't be able to get their entire share.

## Code Snippet
See Vulnerability Detail
## Tool used
Manual Review
## Recommendation
In the `_payment` function, consider updating `lien.amount` after the `beforePayment` call:
```diff
--- a/src/LienToken.sol
+++ b/src/LienToken.sol
@@ -614,12 +614,13 @@ contract LienToken is ERC721, ILienToken, Auth, TransferAgent {
       type(IPublicVault).interfaceId
     );

-    lien.amount = _getOwed(lien);
-
     address payee = getPayee(lienId);
     if (isPublicVault) {
       IPublicVault(lienOwner).beforePayment(lienId, paymentAmount);
     }
+
+    lien.amount = _getOwed(lien);
+
     if (lien.amount > paymentAmount) {
       lien.amount -= paymentAmount;
       lien.last = block.timestamp.safeCastTo32();
```
In this case, lien's slope calculation won't be affected in the `beforePayment` call and the correct slope will be removed from the slope accumulator.