Jeiwan

medium

# Incorrect maximum potential debt calculation causes denial of service

## Summary
Incorrect maximum potential debt calculation causes denial of service
## Vulnerability Detail
According to the documentation ([Loan terms](https://docs.astaria.xyz/docs/protocol-mechanics/loan-terms)):
> Maximum potential debt: The highest amount of debt that could potentially accrued by other existing loans for this collateral. 

According to this definition, maximum potential debt is the sum of owed amounts at expiration dates of all loans for a collateral.
In the code, however, this is implemented differently:

1. During commitment validation ([VaultImplementation.sol#L211-L225](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L211-L225)):
    ```solidity
    uint256 seniorDebt = IAstariaRouter(ROUTER())
      .LIEN_TOKEN()
      .getTotalDebtForCollateralToken(
        params.tokenContract.computeId(params.tokenId)
      );

    ...

    uint256 potentialDebt = seniorDebt * (ld.rate + 1) * ld.duration;
    require(
      potentialDebt <= ld.maxPotentialDebt,
      "Vault._validateCommitment(): Attempting to initiate a loan with debt potentially higher than maxPotentialDebt"
    );
    ```
    Here, total collateral debt is multiplied by `(interest rate + 1) * duration`. This multiplication looks wrong. Also,
    the total collateral debt is calculated at the current timestamp, not at liens' expiration dates ([LienToken.sol#L484](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L484)).
1. During lien creation ([LienToken.sol#L256-L262](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L256-L262)):
    ```solidity
    uint256 totalDebt = getTotalDebtForCollateralToken(collateralId);
    uint256 impliedRate = getImpliedRate(collateralId);

    uint256 potentialDebt = totalDebt *
      (impliedRate + 1) *
      params.terms.duration;

    if (potentialDebt > params.terms.maxPotentialDebt) {
      revert InvalidCollateralState(InvalidStates.DEBT_LIMIT);
    }
    ```
    The same mistakes are made here.


There's a test case that covers this situation ([RevertTesting.t.sol#L106](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/test/RevertTesting.t.sol#L106)):
```solidity
function testFailBorrowMoreThanMaxPotentialDebt() public {
  TestNFT nft = new TestNFT(1);
  address tokenContract = address(nft);
  uint256 tokenId = uint256(0);

  uint256 initialBalance = WETH9.balanceOf(address(this));

  // create a PublicVault with a 14-day epoch
  address publicVault = _createPublicVault({
    strategist: strategistOne,
    delegate: strategistTwo,
    epochLength: 14 days
  });

  // lend 50 ether to the PublicVault as address(1)
  _lendToVault(
    Lender({addr: address(1), amountToLend: 50 ether}),
    publicVault
  );

  // borrow 10 eth against the dummy NFT
  _commitToLien({
    vault: publicVault,
    strategist: strategistOne,
    strategistPK: strategistOnePK,
    tokenContract: tokenContract,
    tokenId: tokenId,
    lienDetails: standardLien,
    amount: 10 ether,
    isFirstLien: true
  });

  _commitToLien({
    vault: publicVault,
    strategist: strategistOne,
    strategistPK: strategistOnePK,
    tokenContract: tokenContract,
    tokenId: tokenId,
    lienDetails: standardLien,
    amount: 10 ether,
    isFirstLien: false
  });
}
```
However, there are a couple of issues with it:
1. The potential debt value that causes the failure in this test is `410958904112640000000000000000000000`, which is not a realistic potential debt.
1. The actual potential debt shouldn't cause a failure: the first loan is 10 ETH taken for 10 days at the rate of 150% per 1 year with maximal potential debt of 50 ETH ([TestHelpers.t.sol#L107-L112](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/test/TestHelpers.t.sol#L107-L112)).
Which means that the expected debt at the expiration date is: (((150% / 365) * 10) / 100) + 1) * 10 ETH = 10.4109 ETH. This is well below the maximal potential debt.
## Impact
Users won't be able to take multiple loans for the same collateral token due to incorrect maximal debt calculation.
## Code Snippet
See Vulnerability Detail
## Tool used
Manual Review
## Recommendation
Consider fixing the maximal debt calculations in the code or updating the documentation to reflect the implementation.