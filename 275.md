Jeiwan

medium

# Only one lien can be created per collateral, vault, and strategy

## Summary
Only one lien can be created per collateral, vault, and strategy
## Vulnerability Detail
Lien ID is generated based on: collateral ID, vault address, loan terms, and strategy root ([LienToken.sol#L264-L279](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L264-L279)). This doesn't include any lien-specific fields, which means duplicate are possible. However, there's no check for whether a lien ID already exists, only a check for the number of liens already created ([LienToken.sol#L282-L285](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L282-L285)). In case a duplicate lien ID is generated, the `_mint` function will revert with "ALREADY_MINTED" error ([LienToken.sol#L289](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L289)).
## Impact
Users cannot take multiple loans for the same collateral token in the same vault and under the same strategy.
## Code Snippet
[LienToken.sol#L264-L303](https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L264-L303):
```solidity
lienId = uint256(
  keccak256(
    abi.encodePacked(
      abi.encode(
        bytes32(collateralId),
        params.vault,
        WETH,
        params.terms.maxAmount,
        params.terms.rate,
        params.terms.duration,
        params.terms.maxPotentialDebt
      ),
      params.strategyRoot
    )
  )
);

//0 - 4 are valid
require(
  uint256(liens[collateralId].length) < MAX_LIENS,
  "too many liens active"
);

uint8 newPosition = uint8(liens[collateralId].length);

_mint(VaultImplementation(params.vault).recipient(), lienId);
lienData[lienId] = Lien({
  collateralId: collateralId,
  position: newPosition,
  amount: params.amount,
  active: true,
  rate: params.terms.rate.safeCastTo240(),
  last: block.timestamp.safeCastTo32(),
  start: block.timestamp.safeCastTo32(),
  duration: params.terms.duration.safeCastTo32(),
  payee: address(0)
});

liens[collateralId].push(lienId);
emit NewLien(lienId, lienData[lienId]);
```
## Tool used
Manual Review
## Recommendation
If only one lien is allowed per collateral, vault, and strategy, consider adding a check for duplicate lien ID with a meaningful error message. If multiple liens are allowed, consider adding a unique and lien-specific value to the lien ID generation.