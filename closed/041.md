Ruhum

medium

# `VaultImplementation.buyoutLien()` will fail because of token mismatch

## Summary
The `VaultImplementation.buyoutLien()` function is unusable if the vault doesn't use WETH as its underlying token.

## Vulnerability Detail
The `VaultImplementation.buyoutLien()` function approves the `LienToken` contract to use its underlying token. But, the `LienToken` contract always tries to transfer WETH. If the vault's token is not WETH, the transfer will fail and the function will be unusable.

## Impact
The user won't be able to trigger the `buyoutLien()` function through the vault contract.

## Code Snippet
https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/VaultImplementation.sol#L280-L304
```sol
function buyoutLien(
  uint256 collateralId,
  uint256 position,
  IAstariaRouter.Commitment calldata incomingTerms
) external whenNotPaused {
  (, uint256 buyout) = IAstariaRouter(ROUTER()).LIEN_TOKEN().getBuyout(
    collateralId,
    position
  );

  require(
    buyout <= ERC20(underlying()).balanceOf(address(this)),
    "not enough balance to buy out loan"
  );

  _validateCommitment(incomingTerms, recipient());

  ERC20(underlying()).safeApprove(
    address(IAstariaRouter(ROUTER()).TRANSFER_PROXY()),
    buyout
  );
  IAstariaRouter(ROUTER()).LIEN_TOKEN().buyoutLien(
    ILienBase.LienActionBuyout(incomingTerms, position, recipient())
  );
}
```

https://github.com/sherlock-audit/2022-10-astaria/blob/main/src/LienToken.sol#L121-L156

```sol
function buyoutLien(ILienToken.LienActionBuyout calldata params) external {
  (bool valid, IAstariaRouter.LienDetails memory ld) = ASTARIA_ROUTER
    .validateCommitment(params.incoming);

  if (!valid) {
    revert InvalidTerms();
  }

  uint256 collateralId = params.incoming.tokenContract.computeId(
    params.incoming.tokenId
  );
  (uint256 owed, uint256 buyout) = getBuyout(collateralId, params.position);
  uint256 lienId = liens[collateralId][params.position];

  //the borrower shouldn't incur more debt from the buyout than they already owe
  if (ld.maxAmount < owed) {
    revert InvalidBuyoutDetails(ld.maxAmount, owed);
  }
  if (!ASTARIA_ROUTER.isValidRefinance(lienData[lienId], ld)) {
    revert InvalidRefinance();
  }

  TRANSFER_PROXY.tokenTransferFrom(
    WETH,
    address(msg.sender),
    getPayee(lienId),
    uint256(buyout)
  );

  lienData[lienId].last = block.timestamp.safeCastTo32();
  lienData[lienId].start = block.timestamp.safeCastTo32();
  lienData[lienId].rate = ld.rate.safeCastTo240();
  lienData[lienId].duration = ld.duration.safeCastTo32();

  _transfer(ownerOf(lienId), address(params.receiver), lienId);
}
```

## Tool used

Manual Review

## Recommendation
Pass the token to the `buyoutLien()` function.